# Maintainer: Sarkasper <echo a2FzcGVyLm1lbnRlbkBnbXguY29tCg== | base64 -d>
# Contributor: Andrew Wong <akamchoiw@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Michalis Georgiou <mechmg93@gmail.comr>
# Contributor: Alexander De Sousa <archaur.xandy21@spamgourmet.com>

pkgname=ttf-google-webfonts
pkgver=20120805
pkgrel=2
pkgdesc="Google Web Fonts catalog."
arch=('any')
url="http://code.google.com/p/googlefontdirectory"
license=('various')
depends=('fontconfig' 'xorg-fonts-encodings')
makedepends=('git')
conflicts=('googlefontdirectory' 'jsmath-fonts' 'lohit-fonts' 'oldstand-font'
           'otf-goudy' 'ttf-andika' 'ttf-anonymous-pro' 'ttf-cantarell'
           'ttf-cardo' 'ttf-chromeos-fonts' 'ttf-droid' 'ttf-inconsolata'
           'ttf-kimberly_geswein_print' 'ttf-oldstandard' 'ttf-pt-sans'
           'ttf-ptsans' 'ttf-roboto' 'ttf-sil-fonts' 'ttf-sortsmillgoudy'
           'ttf-source-sans-pro' 'ttf-ubuntu-font-family' 'ttf-vollkorn')
install=font.install
source=()
md5sums=()

_gitroot="git://github.com/w0ng/googlefontdirectory.git"
_gitname="googlefontdirectory"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone --depth=1 "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
}

package() {
  cd "$srcdir/$_gitname-build"
  install -dm755 "$pkgdir/usr/share/fonts/TTF"
  find . -type f -name \*.ttf -exec install -Dm644 '{}' \
    "$pkgdir/usr/share/fonts/TTF" \;
}
