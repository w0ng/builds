# $Id: PKGBUILD 151284 2012-02-25 12:42:20Z pierre $
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=opengl-man-pages
_svndate=20130425
pkgver=0.${_svndate}
pkgrel=1
pkgdesc="OpenGL Man Pages"
arch=('any')
url="http://www.opengl.org/wiki/Getting_started/XML_Toolchain_and_Man_Pages"
license=('custom')
makedepends=('libxslt' 'docbook-xsl' 'w3c-mathml2' 'docbook-mathml')
options=('!makeflags')
source=("${pkgname}_${_svndate}.tar.xz"
        'LICENSE')
md5sums=('92337f4267fedd87267b3dd1ab9e0494'
         'cb856abe5968300057e650096ba25b2f')

# source PKGBUILD && mksource
mksource() {
  mkdir ${pkgname}-source
  pushd ${pkgname}-source
  [[ -x /usr/bin/svn ]] || (echo "svn not found. Install subversion." && return 1)
  svn co --username anonymous --password anonymous https://cvs.khronos.org/svn/repos/ogl/trunk/ecosystem/public/sdk/docs/man3/ man3
  svn co --username anonymous --password anonymous https://cvs.khronos.org/svn/repos/ogl/trunk/ecosystem/public/sdk/docs/manglsl/ manglsl
  find . -maxdepth 2 -type d -name .svn -exec rm -rf {} \;
  tar -cvJf ../${pkgname}_${_svndate}.tar.xz man3/* manglsl/*
  popd
}

build() {
  export ROOT="${srcdir}/man3"

  mkdir "${srcdir}/build"
  cd "${srcdir}/build"

  make -C ../man3
  make -C ../manglsl

  # Fix XML mismatches
  find  ../manglsl -maxdepth 1 -type f -name \*.xml -exec \
    sed -i 's/#VARTABLECOLS#/<tgroup cols="1"> #VARTABLECOLS#/' {} \;

  find  ../man3 -maxdepth 1 -type f -name \*.xml -exec \
    xsltproc --noout --nonet /usr/share/xml/docbook/xsl-stylesheets-1.78.1/manpages/docbook.xsl {} \;

  find  . -maxdepth 1 -type f -name \*.3G \
    -exec sed -i 's/\[FIXME: manual\]/OpenGL 3.3 Reference/' {} \; \
    -exec sed -i 's/\[FIXME: source\]/opengl-man-pages/' {} \;

  find  ../manglsl -maxdepth 1 -type f -name \*.xml -exec \
    xsltproc --noout --nonet /usr/share/xml/docbook/xsl-stylesheets-1.78.1/manpages/docbook.xsl {} \;

  find  . -maxdepth 1 -type f -name \*.3G \
    -exec sed -i 's/\[FIXME: manual\]/OpenGL Shading Language (GLSL) Reference/' {} \; \
    -exec sed -i 's/\[FIXME: source\]/opengl-man-pages/' {} \;
}

package() {
  cd "${srcdir}/build"
  install -d "${pkgdir}/usr/share/man/man3"
  install -m644 *.3G "${pkgdir}/usr/share/man/man3/"
  install -D -m644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
