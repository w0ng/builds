# $Id: PKGBUILD 166981 2012-09-24 06:32:03Z bisson $
# Contributor: tobias [tobias [at] archlinux.org]
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=mutt
pkgver=1.5.21
pkgrel=8
pkgdesc='Small but very powerful text-based mail client'
url='http://www.mutt.org/'
license=('GPL')
backup=('etc/Muttrc')
arch=('i686' 'x86_64')
optdepends=('smtp-forwarder: to send mail')
depends=('gpgme' 'ncurses' 'openssl' 'libsasl' 'gdbm' 'libidn' 'mime-types' 'krb5')
source=("ftp://ftp.mutt.org/mutt/devel/${pkgname}-${pkgver}.tar.gz"
        'patch-1.5.21-r1.vk.pgp_verbose_mime'
        '1a4c43138685.patch')
sha1sums=('a8475f2618ce5d5d33bff85c0affdf21ab1d76b9'
          '573699f4c4c038b9674714ab7849f9c88aa62059'
          '32dbcafe05e4a919fd00c6a1064c6ecd205e6eb1')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -i ../patch-1.5.21-r1.vk.pgp_verbose_mime
	patch -i ../1a4c43138685.patch # FS#31536
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--enable-gpgme \
		--enable-pop \
		--enable-imap \
		--enable-smtp \
		--enable-hcache \
		--with-curses=/usr \
		--with-regex \
		--with-gss=/usr \
		--with-ssl=/usr \
		--with-sasl \
		--with-idn \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	rm "${pkgdir}"/usr/bin/{flea,muttbug}
	rm "${pkgdir}"/usr/share/man/man1/{flea,muttbug}.1
	rm "${pkgdir}"/etc/mime.types{,.dist}
	install -Dm644 contrib/gpg.rc "${pkgdir}"/etc/Muttrc.gpg.dist
}
