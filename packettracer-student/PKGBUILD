# Maintainer: Andrew Wong <wongadev@gmail.com>
# You must manually download Cisco Package Tracer from:
# https://www.netacad.com/documents/301287/144936315/Cisco+Packet+Tracer+6.1+for+Linux+-+Ubuntu+installation+%28with+tutorials%29%20-+Student+version.tar.gz/d639a1a7-1228-4a9d-8099-2a99096dc594

# Original packettracer AUR package
# Maintainer: Alexey Kharlamov <der@2-47.ru>
# Contributor: David Dufberg TÃ¸ttrup <david at dufberg dot se>
# Contributor: Jordi De Groof <jordi(dot)degroof(at)gmail(dot)com>
# Contributor: pyther <pyther@pyther.net>

pkgname=packettracer-student
pkgver=6.1
pkgrel=1
pkgdesc="Network design and emulation software for Cisco's Networking Academy students."
arch=('i686' 'x86_64')
url="http://www.cisco.com/web/learning/netacad/course_catalog/PacketTracer.html"
license=('custom')
if [[ "$CARCH" = 'x86_64' ]]; then
  depends=('lib32-openssl' 'lib32-qt4' 'lib32-libpng')
  makedepends=('gcc-multilib')
elif [[ "$CARCH" = 'i686' ]]; then
  depends=('qtwebkit' 'libpng')
fi
source=('Cisco Packet Tracer 6.1 for Linux - Ubuntu installation (with tutorials) - Student version.tar.gz' 
        'tpl.linguist'
        'tpl.packettracer')
md5sums=('bb0807a52147701417afb7aaaa5110ec'
         '44911e33930d8469eb2240cc807d4a32'
         '1e6109dea412e21f63054541614235fd')

# We don't want to strip anything from the static libraries
# We want to keep all binaries orginal (Cisco is goofy)
options=(!strip)
install=pt.install

package() {
  cd ${srcdir}/PacketTracer61Student

  mkdir -p ${pkgdir}/opt/pt/{art,backgrounds,bin,extensions,help,LANGUAGES,lib,saves,sounds,templates}

  cp -r ./art/* ${pkgdir}/opt/pt/art
  cp -r ./backgrounds/* ${pkgdir}/opt/pt/backgrounds
  cp -r ./bin/* ${pkgdir}/opt/pt/bin
  cp -r ./extensions/* ${pkgdir}/opt/pt/extensions
  cp -r ./help/* ${pkgdir}/opt/pt/help
  cp -r ./LANGUAGES/* ${pkgdir}/opt/pt/LANGUAGES
  cp -r ./lib/* ${pkgdir}/opt/pt/lib
  cp -r ./saves/* ${pkgdir}/opt/pt/saves
  cp -r ./sounds/* ${pkgdir}/opt/pt/sounds
  cp -r ./templates/* ${pkgdir}/opt/pt/templates

  # Mime Info for PKA, PKT, PKZ
  install -D -m644 ./bin/Cisco-pka.xml ${pkgdir}/usr/share/mime/packages/Cisco-pka.xml
  install -D -m644 ./bin/Cisco-pkt.xml ${pkgdir}/usr/share/mime/packages/Cisco-pkt.xml
  install -D -m644 ./bin/Cisco-pkz.xml ${pkgdir}/usr/share/mime/packages/Cisco-pkz.xml

  rm ${pkgdir}/opt/pt/bin/Cisco-pka.xml
  rm ${pkgdir}/opt/pt/bin/Cisco-pkt.xml
  rm ${pkgdir}/opt/pt/bin/Cisco-pkz.xml

  # Install Mimetype Icons
  install -D -m644 ./art/pka.png ${pkgdir}/usr/share/icons/hicolor/48x48/mimetypes/application-x-pka.png
  install -D -m644 ./art/pkt.png ${pkgdir}/usr/share/icons/hicolor/48x48/mimetypes/application-x-pkt.png
  install -D -m644 ./art/pkz.png ${pkgdir}/usr/share/icons/hicolor/48x48/mimetypes/application-x-pkz.png

  # EULA
  install -D -m644 eula.txt ${pkgdir}/usr/share/licenses/$pkgname/eula.txt

  # Shell script to start PT and tell it to use included qt files
  # Arch's QT causes PT to crash when saving!
  install -D -m755 ${srcdir}/tpl.packettracer ${pkgdir}/opt/pt/packettracer

  # Symlink to /usr/bin
  mkdir -p ${pkgdir}/usr/bin/
  ln -s /opt/pt/packettracer ${pkgdir}/usr/bin/pt

  # Improved version of Cisco's linguist script
  install -D -m755 ${srcdir}/tpl.linguist ${pkgdir}/opt/pt/linguist

  # Desktop File
  install -D -m644 ./bin/Cisco-PacketTracer.desktop ${pkgdir}/usr/share/applications/Cisco-PacketTracer.desktop
  sed 's/\/usr\/local\/PacketTracer6/\/opt\/pt/' -i ${pkgdir}/usr/share/applications/Cisco-PacketTracer.desktop 
  rm ${pkgdir}/opt/pt/bin/Cisco-PacketTracer.desktop
}
